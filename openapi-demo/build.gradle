import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'org.springframework.boot' version '2.7.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'java'

    // applying the openapi generator plugin
    id "org.openapi.generator" version "6.2.1"
}

group = 'com.tmsvr'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

// adding the generated sources to the source sets
sourceSets.main.java.srcDirs += 'build/generated/petstore/src/main/java'
sourceSets.main.java.srcDirs += 'build/generated/uuid/src/main/java'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // required for generated openapi stuff
    implementation "javax.validation:validation-api:2.0.1.Final"
    implementation "io.springfox:springfox-swagger2:3.0.0"
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    annotationProcessor 'io.swagger:swagger-annotations:1.6.8'
}

// 2 new tasks to generate the client and server code
task buildUuidClient(type: GenerateTask) {
    generatorName = "java"
    groupId = "com.tmsvr.api"
    id = "uuidapi"
    inputSpec = "$rootDir/uuid.yml".toString()
    outputDir = "$buildDir/generated/uuid".toString()
    apiPackage = "com.tmsvr.openapidemo.uuid.api"
    invokerPackage = "com.tmsvr.openapidemo.uuid.invoker"
    modelPackage = "com.tmsvr.openapidemo.uuid.model"
    configOptions = [
            dateLibrary: "java8",
            library: "resttemplate",
            serializableModel   : "true",
            hideGenerationTimestamp : "true"
    ]
}

task buildPetStoreServer(type: GenerateTask) {
    generatorName = "spring"
    groupId = "$project.group"
    id = "$project.name-java-client"
    version = "$project.version"
    inputSpec = "$rootDir/petstore.yml".toString()
    outputDir = "$buildDir/generated/petstore".toString()
    apiPackage = "com.tmsvr.openapidemo.petstore.api"
    invokerPackage = "com.tmsvr.openapidemo.petstore.invoker"
    modelPackage = "com.tmsvr.openapidemo.petstore.model"
    generateApiDocumentation = false
    generateModelDocumentation = false
    generateModelTests = false
    generateApiTests = false
    configOptions = [
            java8               : "true",
            dateLibrary         : "java8",
            serializationLibrary: "jackson",
            library             : "spring-boot",
            useBeanValidation   : "true",
            interfaceOnly       : "true",
            serializableModel   : "true",
            useTags             : "true"
    ]
}

// make compileJava run the code generation
compileJava.dependsOn tasks.buildPetStoreServer, tasks.buildUuidClient

test {
    useJUnitPlatform()
}
